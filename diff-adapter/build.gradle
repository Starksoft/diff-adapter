apply plugin: 'com.android.library'
apply plugin: 'kotlin-android'
apply plugin: 'kotlin-android-extensions'
apply plugin: 'com.github.dcendents.android-maven'
apply plugin: 'com.jfrog.bintray'

android {

	defaultConfig {
		defaultPublishConfig "release"

		compileSdkVersion buildConfig.compileSdk
		minSdkVersion buildConfig.minSdk
		targetSdkVersion buildConfig.targetSdk
		testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
	}

	compileOptions {
		sourceCompatibility JavaVersion.VERSION_1_8
		targetCompatibility JavaVersion.VERSION_1_8
	}

	buildTypes {
		release {
			minifyEnabled false
			proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
		}
		debug {
			minifyEnabled false
			debuggable true
		}
	}

	buildToolsVersion versions.buildTools

	lintOptions {
		abortOnError false
		tasks.lint.enabled = rootProject.buildConfig.lintEnabled
	}

	dexOptions {
		preDexLibraries false
	}
}

configurations.all {
	resolutionStrategy.force "com.android.support:recyclerview-v7:$versions.supportLibrary"
}

ext.artifact = [
		'groupId': 'ru.starksoft',
		'id'     : 'diff-adapter',
		'version': '1.0.5',
		'siteUrl': 'https://github.com/Starksoft/diff-adapter',
		'gitUrl' : 'https://github.com/Starksoft/diff-adapter.git',
		'repo'   : 'android-maven'
]

version = artifact.version
group = artifact.groupId

install {
	repositories.mavenInstaller {
		pom.project {
			packaging 'aar'
			groupId artifact.groupId
			artifactId artifact.id
			version artifact.version
			name artifact.id // pom.project.name must be same as bintray.pkg.name
			url artifact.siteUrl
			inceptionYear '2019' // HARDCODED
			licenses {
				license { // HARDCODED
					name 'GPL-3.0'
					url 'https://www.gnu.org/licenses/gpl.txt'
					distribution 'repo'
				}
			}
			scm {
				connection artifact.gitUrl
				developerConnection artifact.gitUrl
				url artifact.siteUrl
			}
		}
	}
}

task sourcesJar(type: Jar) {
	from android.sourceSets.main.java.srcDirs
	classifier = "sources"
}

task javadoc(type: Javadoc) {
	source = android.sourceSets.main.java.srcDirs
	classpath += project.files(android.getBootClasspath().join(File.pathSeparator))
}

task javadocJar(type: Jar, dependsOn: javadoc) {
	classifier = "javadoc"
	from javadoc.destinationDir
	// options.encoding = ‘UTF-8’
}

artifacts {
	archives sourcesJar
//	archives javadocJar
}

bintray {
	user = System.getenv('user')
	key = System.getenv('key')

	configurations = ['archives']
	pkg {
		repo = artifact.repo
		name = artifact.id
		websiteUrl = artifact.siteUrl
		vcsUrl = artifact.gitUrl
		userOrg = artifact.userOrg
		licenses = ['Apache-2.0']
		publish = true
		version {
			name = artifact.version
		}
	}
}

dependencies {
	implementation fileTree(dir: 'libs', include: ['*.jar'])

	implementation 'androidx.recyclerview:recyclerview:1.1.0-rc01'
	implementation "androidx.annotation:annotation:1.1.0"
	implementation "androidx.core:core-ktx:1.1.0"
	implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk7:$versions.kotlin"
}
